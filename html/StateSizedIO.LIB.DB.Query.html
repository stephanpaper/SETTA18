<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>StateSizedIO.LIB.DB.Query</title><link rel="stylesheet" href="Agda.css"></head><body><pre>
<a id="2" class="Keyword">module</a> <a id="9" href="StateSizedIO.LIB.DB.Query.html" class="Module">StateSizedIO.LIB.DB.Query</a> <a id="35" class="Keyword">where</a>

<a id="42" class="Keyword">open</a> <a id="47" class="Keyword">import</a> <a id="54" href="Data.Product.html" class="Module">Data.Product</a> <a id="67" class="Keyword">renaming</a> <a id="76" class="Symbol">(</a><a id="77" href="Data.Product.html#559" class="Field">proj₁</a> <a id="83" class="Symbol">to</a> <a id="86" href="Data.Product.html#559" class="Field">fst</a><a id="89" class="Symbol">;</a> <a id="91" href="Data.Product.html#573" class="Field">proj₂</a> <a id="97" class="Symbol">to</a> <a id="100" href="Data.Product.html#573" class="Field">snd</a><a id="103" class="Symbol">)</a>
<a id="105" class="Keyword">open</a> <a id="110" class="Keyword">import</a> <a id="117" href="Data.Bool.html" class="Module">Data.Bool</a>
<a id="127" class="Keyword">open</a> <a id="132" class="Keyword">import</a> <a id="139" href="Data.String.html" class="Module">Data.String</a> <a id="151" class="Keyword">renaming</a> <a id="160" class="Symbol">(</a><a id="161" href="Data.String.Base.html#1101" class="Function Operator">_++_</a> <a id="166" class="Symbol">to</a> <a id="169" href="Data.String.Base.html#1101" class="Function Operator">_+++_</a><a id="174" class="Symbol">)</a> <a id="176" class="Keyword">hiding</a> <a id="183" class="Symbol">(</a><a id="184" href="Data.String.Base.html#1371" class="Function">show</a><a id="188" class="Symbol">)</a>
<a id="190" class="Keyword">open</a> <a id="195" class="Keyword">import</a> <a id="202" href="Data.List.html" class="Module">Data.List</a> <a id="212" class="Keyword">hiding</a> <a id="219" class="Symbol">(</a><a id="220" href="Data.List.Base.html#4747" class="Function">lookup</a><a id="226" class="Symbol">)</a>
<a id="228" class="Keyword">open</a> <a id="233" class="Keyword">import</a> <a id="240" href="Data.Vec.html" class="Module">Data.Vec</a> <a id="249" class="Keyword">hiding</a> <a id="256" class="Symbol">(</a><a id="257" href="Data.Vec.html#1471" class="Function">lookup</a><a id="263" class="Symbol">;</a> <a id="265" href="Data.Vec.html#2027" class="Function Operator">_++_</a><a id="269" class="Symbol">)</a>
<a id="271" class="Keyword">open</a> <a id="276" class="Keyword">import</a> <a id="283" href="Function.html" class="Module">Function</a>
<a id="292" class="Keyword">open</a> <a id="297" class="Keyword">import</a> <a id="304" href="Data.Integer.html" class="Module">Data.Integer</a> <a id="317" class="Keyword">renaming</a> <a id="326" class="Symbol">(</a><a id="327" href="Data.Integer.html#941" class="Function">show</a> <a id="332" class="Symbol">to</a> <a id="335" href="Data.Integer.html#941" class="Function">showℤ</a><a id="340" class="Symbol">)</a>

<a id="343" class="Keyword">import</a> <a id="350" href="StateSizedIO.LIB.DB.Dictionary.html" class="Module">StateSizedIO.LIB.DB.Dictionary</a>

<a id="382" class="Keyword">open</a> <a id="387" class="Keyword">import</a> <a id="394" href="StateSizedIO.LIB.DB.Schema.html" class="Module">StateSizedIO.LIB.DB.Schema</a>
<a id="421" class="Comment">--open import Universe</a>
<a id="444" class="Keyword">open</a> <a id="449" href="StateSizedIO.LIB.DB.Dictionary.html" class="Module">StateSizedIO.LIB.DB.Dictionary</a> <a id="480" class="Symbol">{</a><a id="481" href="Agda.Builtin.String.html#165" class="Postulate">String</a><a id="487" class="Symbol">}</a> <a id="489" href="Data.String.html#1639" class="Function Operator">_==_</a>
<a id="494" class="Keyword">open</a> <a id="499" class="Keyword">import</a> <a id="506" href="StateSizedIO.LIB.DB.DBUniverse.html" class="Module">StateSizedIO.LIB.DB.DBUniverse</a>



<a id="540" class="Keyword">data</a> <a id="ExprType"></a><a id="545" href="StateSizedIO.LIB.DB.Query.html#545" class="Datatype">ExprType</a> <a id="554" class="Symbol">:</a> <a id="556" class="PrimitiveType">Set</a> <a id="560" class="Keyword">where</a>
  <a id="ExprType.bool"></a><a id="568" href="StateSizedIO.LIB.DB.Query.html#568" class="InductiveConstructor">bool</a>  <a id="574" class="Symbol">:</a> <a id="576" href="StateSizedIO.LIB.DB.Query.html#545" class="Datatype">ExprType</a>
  <a id="ExprType.ftype"></a><a id="587" href="StateSizedIO.LIB.DB.Query.html#587" class="InductiveConstructor">ftype</a> <a id="593" class="Symbol">:</a> <a id="595" href="StateSizedIO.LIB.DB.Schema.html#463" class="Datatype">FieldType</a> <a id="605" class="Symbol">-&gt;</a> <a id="608" href="StateSizedIO.LIB.DB.Query.html#545" class="Datatype">ExprType</a>

<a id="618" class="Comment">-- Exercise: add more interesting expressions.</a>
<a id="665" class="Comment">-- For instance, less than or greater than comparisons. Look at the SQL documentation for ideas.</a>



<a id="765" class="Comment">-- Expressions are</a>
<a id="784" class="Comment">--   ! fieldname       fieldname is a string for a field in a table</a>
<a id="852" class="Comment">--   val v             v is value for a field</a>
<a id="898" class="Comment">--   s == t            equality between two expressions</a>
<a id="954" class="Comment">-- If a table has for instance field  &quot;name&quot; of type string</a>
<a id="1014" class="Comment">-- expressions could be</a>
<a id="1038" class="Comment">-- ! &quot;name&quot; == &quot;Tom&quot;</a>
<a id="1059" class="Comment">-- standing for the expression   name = &quot;Tom&quot;</a>


<a id="1107" class="Keyword">data</a> <a id="Expr"></a><a id="1112" href="StateSizedIO.LIB.DB.Query.html#1112" class="Datatype">Expr</a> <a id="1117" class="Symbol">(</a><a id="1118" href="StateSizedIO.LIB.DB.Query.html#1118" class="Bound">t</a> <a id="1120" class="Symbol">:</a> <a id="1122" href="StateSizedIO.LIB.DB.Schema.html#800" class="Function">TableType</a><a id="1131" class="Symbol">)</a> <a id="1133" class="Symbol">:</a> <a id="1135" href="StateSizedIO.LIB.DB.Query.html#545" class="Datatype">ExprType</a> <a id="1144" class="Symbol">-&gt;</a> <a id="1147" class="PrimitiveType">Set</a> <a id="1151" class="Keyword">where</a>
  <a id="Expr._===_"></a><a id="1159" href="StateSizedIO.LIB.DB.Query.html#1159" class="InductiveConstructor Operator">_===_</a> <a id="1165" class="Symbol">:</a> <a id="1167" class="Symbol">forall</a> <a id="1174" class="Symbol">{</a><a id="1175" href="StateSizedIO.LIB.DB.Query.html#1175" class="Bound">a</a><a id="1176" class="Symbol">}</a> <a id="1178" class="Symbol">-&gt;</a> <a id="1181" href="StateSizedIO.LIB.DB.Query.html#1112" class="Datatype">Expr</a> <a id="1186" href="StateSizedIO.LIB.DB.Query.html#1118" class="Bound">t</a> <a id="1188" href="StateSizedIO.LIB.DB.Query.html#1175" class="Bound">a</a> <a id="1190" class="Symbol">-&gt;</a> <a id="1193" href="StateSizedIO.LIB.DB.Query.html#1112" class="Datatype">Expr</a> <a id="1198" href="StateSizedIO.LIB.DB.Query.html#1118" class="Bound">t</a> <a id="1200" href="StateSizedIO.LIB.DB.Query.html#1175" class="Bound">a</a> <a id="1202" class="Symbol">-&gt;</a> <a id="1205" href="StateSizedIO.LIB.DB.Query.html#1112" class="Datatype">Expr</a> <a id="1210" href="StateSizedIO.LIB.DB.Query.html#1118" class="Bound">t</a> <a id="1212" href="StateSizedIO.LIB.DB.Query.html#568" class="InductiveConstructor">bool</a>
  <a id="Expr.!_"></a><a id="1219" href="StateSizedIO.LIB.DB.Query.html#1219" class="InductiveConstructor Operator">!_</a>    <a id="1225" class="Symbol">:</a> <a id="1227" class="Symbol">forall</a> <a id="1234" href="StateSizedIO.LIB.DB.Query.html#1234" class="Bound">x</a> <a id="1236" class="Symbol">{</a><a id="1237" href="StateSizedIO.LIB.DB.Query.html#1237" class="Bound">p</a> <a id="1239" class="Symbol">:</a> <a id="1241" href="StateSizedIO.LIB.DB.Dictionary.html#463" class="Function">HasKey</a> <a id="1248" href="StateSizedIO.LIB.DB.Query.html#1234" class="Bound">x</a> <a id="1250" href="StateSizedIO.LIB.DB.Query.html#1118" class="Bound">t</a><a id="1251" class="Symbol">}</a> <a id="1253" class="Symbol">-&gt;</a> <a id="1256" href="StateSizedIO.LIB.DB.Query.html#1112" class="Datatype">Expr</a> <a id="1261" href="StateSizedIO.LIB.DB.Query.html#1118" class="Bound">t</a> <a id="1263" class="Symbol">(</a><a id="1264" href="StateSizedIO.LIB.DB.Query.html#587" class="InductiveConstructor">ftype</a> <a id="1270" class="Symbol">(</a><a id="1271" href="Data.Product.html#559" class="Field">fst</a> <a id="1275" class="Symbol">(</a><a id="1276" href="StateSizedIO.LIB.DB.Dictionary.html#537" class="Function">lookup</a> <a id="1283" href="StateSizedIO.LIB.DB.Query.html#1234" class="Bound">x</a> <a id="1285" href="StateSizedIO.LIB.DB.Query.html#1118" class="Bound">t</a> <a id="1287" class="Symbol">{</a><a id="1288" href="StateSizedIO.LIB.DB.Query.html#1237" class="Bound">p</a><a id="1289" class="Symbol">})))</a>
  <a id="Expr.val"></a><a id="1296" href="StateSizedIO.LIB.DB.Query.html#1296" class="InductiveConstructor">val</a>   <a id="1302" class="Symbol">:</a> <a id="1304" class="Symbol">forall</a> <a id="1311" class="Symbol">{</a><a id="1312" href="StateSizedIO.LIB.DB.Query.html#1312" class="Bound">a</a><a id="1313" class="Symbol">}</a> <a id="1315" class="Symbol">-&gt;</a> <a id="1318" href="StateSizedIO.LIB.DB.Schema.html#2877" class="Function">Field</a> <a id="1324" href="StateSizedIO.LIB.DB.Query.html#1312" class="Bound">a</a> <a id="1326" class="Symbol">-&gt;</a> <a id="1329" href="StateSizedIO.LIB.DB.Query.html#1112" class="Datatype">Expr</a> <a id="1334" href="StateSizedIO.LIB.DB.Query.html#1118" class="Bound">t</a> <a id="1336" class="Symbol">(</a><a id="1337" href="StateSizedIO.LIB.DB.Query.html#587" class="InductiveConstructor">ftype</a> <a id="1343" href="StateSizedIO.LIB.DB.Query.html#1312" class="Bound">a</a><a id="1344" class="Symbol">)</a>

<a id="1347" class="Comment">-- Exercise: add more interesting queries. For instance,</a>
<a id="1404" class="Comment">-- 1. Make it possible to rename fields to allow combining tables with overlapping column names.</a>
<a id="1501" class="Comment">-- 2. SQL supports counting and ordering results. Add this to our queries.</a>


<a id="1578" class="Comment">-- Queries are</a>
<a id="1593" class="Comment">--  read &quot;tablename&quot;</a>
<a id="1614" class="Comment">--       corresponding  to select tablename</a>
<a id="1658" class="Comment">--  p ⊗ p&#39;    which is an outer join of two queries</a>
<a id="1710" class="Comment">--  select t q     ist</a>
<a id="1733" class="Comment">--                query q where t</a>
<a id="1767" class="Comment">--  proj as q</a>
<a id="1781" class="Comment">--    is where you select one field in a query</a>
<a id="1828" class="Comment">--</a>
<a id="1831" class="Keyword">data</a> <a id="Query"></a><a id="1836" href="StateSizedIO.LIB.DB.Query.html#1836" class="Datatype">Query</a> <a id="1842" class="Symbol">(</a><a id="1843" href="StateSizedIO.LIB.DB.Query.html#1843" class="Bound">s</a> <a id="1845" class="Symbol">:</a> <a id="1847" href="StateSizedIO.LIB.DB.Schema.html#827" class="Function">Schema</a><a id="1853" class="Symbol">)</a> <a id="1855" class="Symbol">:</a> <a id="1857" href="StateSizedIO.LIB.DB.Schema.html#800" class="Function">TableType</a> <a id="1867" class="Symbol">-&gt;</a> <a id="1870" class="PrimitiveType">Set</a> <a id="1874" class="Keyword">where</a>
  <a id="Query.read"></a><a id="1882" href="StateSizedIO.LIB.DB.Query.html#1882" class="InductiveConstructor">read</a>   <a id="1889" class="Symbol">:</a> <a id="1891" class="Symbol">(</a><a id="1892" href="StateSizedIO.LIB.DB.Query.html#1892" class="Bound">x</a> <a id="1894" class="Symbol">:</a> <a id="1896" href="StateSizedIO.LIB.DB.Schema.html#667" class="Function">TableName</a><a id="1905" class="Symbol">){</a><a id="1907" href="StateSizedIO.LIB.DB.Query.html#1907" class="Bound">p</a> <a id="1909" class="Symbol">:</a> <a id="1911" href="StateSizedIO.LIB.DB.Dictionary.html#463" class="Function">HasKey</a> <a id="1918" href="StateSizedIO.LIB.DB.Query.html#1892" class="Bound">x</a> <a id="1920" href="StateSizedIO.LIB.DB.Query.html#1843" class="Bound">s</a><a id="1921" class="Symbol">}</a> <a id="1923" class="Symbol">-&gt;</a> <a id="1926" href="StateSizedIO.LIB.DB.Query.html#1836" class="Datatype">Query</a> <a id="1932" href="StateSizedIO.LIB.DB.Query.html#1843" class="Bound">s</a> <a id="1934" class="Symbol">(</a><a id="1935" href="StateSizedIO.LIB.DB.Dictionary.html#537" class="Function">lookup</a> <a id="1942" href="StateSizedIO.LIB.DB.Query.html#1892" class="Bound">x</a> <a id="1944" href="StateSizedIO.LIB.DB.Query.html#1843" class="Bound">s</a> <a id="1946" class="Symbol">{</a><a id="1947" href="StateSizedIO.LIB.DB.Query.html#1907" class="Bound">p</a><a id="1948" class="Symbol">})</a>
  <a id="Query._⊗_"></a><a id="1953" href="StateSizedIO.LIB.DB.Query.html#1953" class="InductiveConstructor Operator">_⊗_</a>    <a id="1960" class="Symbol">:</a> <a id="1962" class="Symbol">forall</a> <a id="1969" class="Symbol">{</a><a id="1970" href="StateSizedIO.LIB.DB.Query.html#1970" class="Bound">t</a> <a id="1972" href="StateSizedIO.LIB.DB.Query.html#1972" class="Bound">t&#39;</a><a id="1974" class="Symbol">}{</a><a id="1976" href="StateSizedIO.LIB.DB.Query.html#1976" class="Bound">p</a> <a id="1978" class="Symbol">:</a> <a id="1980" href="StateSizedIO.LIB.DB.Dictionary.html#1570" class="Function">Disjoint</a> <a id="1989" href="StateSizedIO.LIB.DB.Query.html#1970" class="Bound">t</a> <a id="1991" href="StateSizedIO.LIB.DB.Query.html#1972" class="Bound">t&#39;</a><a id="1993" class="Symbol">}</a> <a id="1995" class="Symbol">-&gt;</a>
           <a id="2009" href="StateSizedIO.LIB.DB.Query.html#1836" class="Datatype">Query</a> <a id="2015" href="StateSizedIO.LIB.DB.Query.html#1843" class="Bound">s</a> <a id="2017" href="StateSizedIO.LIB.DB.Query.html#1970" class="Bound">t</a> <a id="2019" class="Symbol">-&gt;</a> <a id="2022" href="StateSizedIO.LIB.DB.Query.html#1836" class="Datatype">Query</a> <a id="2028" href="StateSizedIO.LIB.DB.Query.html#1843" class="Bound">s</a> <a id="2030" href="StateSizedIO.LIB.DB.Query.html#1972" class="Bound">t&#39;</a> <a id="2033" class="Symbol">-&gt;</a> <a id="2036" href="StateSizedIO.LIB.DB.Query.html#1836" class="Datatype">Query</a> <a id="2042" href="StateSizedIO.LIB.DB.Query.html#1843" class="Bound">s</a> <a id="2044" class="Symbol">(</a><a id="2045" href="StateSizedIO.LIB.DB.Query.html#1970" class="Bound">t</a> <a id="2047" href="Data.List.Base.html#1391" class="Function Operator">++</a> <a id="2050" href="StateSizedIO.LIB.DB.Query.html#1972" class="Bound">t&#39;</a><a id="2052" class="Symbol">)</a>
  <a id="Query.proj"></a><a id="2056" href="StateSizedIO.LIB.DB.Query.html#2056" class="InductiveConstructor">proj</a>   <a id="2063" class="Symbol">:</a> <a id="2065" class="Symbol">forall</a> <a id="2072" class="Symbol">{</a><a id="2073" href="StateSizedIO.LIB.DB.Query.html#2073" class="Bound">t</a><a id="2074" class="Symbol">}</a> <a id="2076" href="StateSizedIO.LIB.DB.Query.html#2076" class="Bound">xs</a> <a id="2079" class="Symbol">{</a><a id="2080" href="StateSizedIO.LIB.DB.Query.html#2080" class="Bound">p</a> <a id="2082" class="Symbol">:</a> <a id="2084" href="StateSizedIO.LIB.DB.Dictionary.html#827" class="Function">HasKeys</a> <a id="2092" href="StateSizedIO.LIB.DB.Query.html#2076" class="Bound">xs</a> <a id="2095" href="StateSizedIO.LIB.DB.Query.html#2073" class="Bound">t</a><a id="2096" class="Symbol">}</a> <a id="2098" class="Symbol">-&gt;</a>
           <a id="2112" href="StateSizedIO.LIB.DB.Query.html#1836" class="Datatype">Query</a> <a id="2118" href="StateSizedIO.LIB.DB.Query.html#1843" class="Bound">s</a> <a id="2120" href="StateSizedIO.LIB.DB.Query.html#2073" class="Bound">t</a> <a id="2122" class="Symbol">-&gt;</a> <a id="2125" href="StateSizedIO.LIB.DB.Query.html#1836" class="Datatype">Query</a> <a id="2131" href="StateSizedIO.LIB.DB.Query.html#1843" class="Bound">s</a> <a id="2133" class="Symbol">(</a><a id="2134" href="StateSizedIO.LIB.DB.Dictionary.html#1080" class="Function">project</a> <a id="2142" href="StateSizedIO.LIB.DB.Query.html#2076" class="Bound">xs</a> <a id="2145" href="StateSizedIO.LIB.DB.Query.html#2073" class="Bound">t</a> <a id="2147" class="Symbol">{</a><a id="2148" href="StateSizedIO.LIB.DB.Query.html#2080" class="Bound">p</a><a id="2149" class="Symbol">})</a>
  <a id="Query.select"></a><a id="2154" href="StateSizedIO.LIB.DB.Query.html#2154" class="InductiveConstructor">select</a> <a id="2161" class="Symbol">:</a> <a id="2163" class="Symbol">forall</a> <a id="2170" class="Symbol">{</a><a id="2171" href="StateSizedIO.LIB.DB.Query.html#2171" class="Bound">t</a><a id="2172" class="Symbol">}</a> <a id="2174" class="Symbol">-&gt;</a> <a id="2177" href="StateSizedIO.LIB.DB.Query.html#1112" class="Datatype">Expr</a> <a id="2182" href="StateSizedIO.LIB.DB.Query.html#2171" class="Bound">t</a> <a id="2184" href="StateSizedIO.LIB.DB.Query.html#568" class="InductiveConstructor">bool</a> <a id="2189" class="Symbol">-&gt;</a> <a id="2192" href="StateSizedIO.LIB.DB.Query.html#1836" class="Datatype">Query</a> <a id="2198" href="StateSizedIO.LIB.DB.Query.html#1843" class="Bound">s</a> <a id="2200" href="StateSizedIO.LIB.DB.Query.html#2171" class="Bound">t</a> <a id="2202" class="Symbol">-&gt;</a> <a id="2205" href="StateSizedIO.LIB.DB.Query.html#1836" class="Datatype">Query</a> <a id="2211" href="StateSizedIO.LIB.DB.Query.html#1843" class="Bound">s</a> <a id="2213" href="StateSizedIO.LIB.DB.Query.html#2171" class="Bound">t</a>

<a id="2216" class="Comment">-- This might have to be changed when you add more interesting queries.</a>

<a id="2289" class="Keyword">data</a> <a id="SQL"></a><a id="2294" href="StateSizedIO.LIB.DB.Query.html#2294" class="Datatype">SQL</a> <a id="2298" class="Symbol">:</a> <a id="2300" class="PrimitiveType">Set</a> <a id="2304" class="Keyword">where</a>
  <a id="SQL.SELECT_FROM_WHERE_"></a><a id="2312" href="StateSizedIO.LIB.DB.Query.html#2312" class="InductiveConstructor Operator">SELECT_FROM_WHERE_</a> <a id="2331" class="Symbol">:</a> <a id="2333" href="Agda.Builtin.List.html#80" class="Datatype">List</a> <a id="2338" href="StateSizedIO.LIB.DB.Schema.html#686" class="Function">FieldName</a> <a id="2348" class="Symbol">-&gt;</a> <a id="2351" href="Agda.Builtin.List.html#80" class="Datatype">List</a> <a id="2356" href="StateSizedIO.LIB.DB.Schema.html#667" class="Function">TableName</a> <a id="2366" class="Symbol">-&gt;</a> <a id="2369" href="Agda.Builtin.List.html#80" class="Datatype">List</a> <a id="2374" href="Agda.Builtin.String.html#165" class="Postulate">String</a> <a id="2381" class="Symbol">-&gt;</a> <a id="2384" href="StateSizedIO.LIB.DB.Query.html#2294" class="Datatype">SQL</a>


<a id="2390" class="Comment">-- sepBy takes a list of strings, concatenates them but putting in between</a>
<a id="2465" class="Comment">--   the first argument.</a>
<a id="sepBy"></a><a id="2490" href="StateSizedIO.LIB.DB.Query.html#2490" class="Function">sepBy</a> <a id="2496" class="Symbol">:</a> <a id="2498" href="Agda.Builtin.String.html#165" class="Postulate">String</a> <a id="2505" class="Symbol">-&gt;</a> <a id="2508" href="Agda.Builtin.List.html#80" class="Datatype">List</a> <a id="2513" href="Agda.Builtin.String.html#165" class="Postulate">String</a> <a id="2520" class="Symbol">-&gt;</a> <a id="2523" href="Agda.Builtin.String.html#165" class="Postulate">String</a>
<a id="2530" href="StateSizedIO.LIB.DB.Query.html#2490" class="Function">sepBy</a> <a id="2536" class="Symbol">_</a> <a id="2538" href="Agda.Builtin.List.html#117" class="InductiveConstructor">[]</a> <a id="2541" class="Symbol">=</a> <a id="2543" class="String">&quot;&quot;</a>
<a id="2546" href="StateSizedIO.LIB.DB.Query.html#2490" class="Function">sepBy</a> <a id="2552" class="Symbol">_</a> <a id="2554" class="Symbol">(</a><a id="2555" href="StateSizedIO.LIB.DB.Query.html#2555" class="Bound">x</a> <a id="2557" href="Agda.Builtin.List.html#132" class="InductiveConstructor Operator">∷</a> <a id="2559" href="Agda.Builtin.List.html#117" class="InductiveConstructor">[]</a><a id="2561" class="Symbol">)</a> <a id="2563" class="Symbol">=</a> <a id="2565" href="StateSizedIO.LIB.DB.Query.html#2555" class="Bound">x</a>
<a id="2567" href="StateSizedIO.LIB.DB.Query.html#2490" class="CatchallClause Function">sepBy</a><a id="2572" class="CatchallClause"> </a><a id="2573" href="StateSizedIO.LIB.DB.Query.html#2573" class="CatchallClause Bound">i</a><a id="2574" class="CatchallClause"> </a><a id="2575" class="CatchallClause Symbol">(</a><a id="2576" href="StateSizedIO.LIB.DB.Query.html#2576" class="CatchallClause Bound">x</a><a id="2577" class="CatchallClause"> </a><a id="2578" href="Agda.Builtin.List.html#132" class="CatchallClause InductiveConstructor Operator">∷</a><a id="2579" class="CatchallClause"> </a><a id="2580" href="StateSizedIO.LIB.DB.Query.html#2580" class="CatchallClause Bound">xs</a><a id="2582" class="CatchallClause Symbol">)</a> <a id="2584" class="Symbol">=</a> <a id="2586" href="StateSizedIO.LIB.DB.Query.html#2576" class="Bound">x</a> <a id="2588" href="Data.String.Base.html#1101" class="Function Operator">+++</a> <a id="2592" href="StateSizedIO.LIB.DB.Query.html#2573" class="Bound">i</a> <a id="2594" href="Data.String.Base.html#1101" class="Function Operator">+++</a> <a id="2598" href="StateSizedIO.LIB.DB.Query.html#2490" class="Function">sepBy</a> <a id="2604" href="StateSizedIO.LIB.DB.Query.html#2573" class="Bound">i</a> <a id="2606" href="StateSizedIO.LIB.DB.Query.html#2580" class="Bound">xs</a>

<a id="showField"></a><a id="2610" href="StateSizedIO.LIB.DB.Query.html#2610" class="Function">showField</a> <a id="2620" class="Symbol">:</a> <a id="2622" class="Symbol">forall</a> <a id="2629" class="Symbol">{</a><a id="2630" href="StateSizedIO.LIB.DB.Query.html#2630" class="Bound">a</a><a id="2631" class="Symbol">}</a> <a id="2633" class="Symbol">-&gt;</a> <a id="2636" href="StateSizedIO.LIB.DB.Schema.html#2877" class="Function">Field</a> <a id="2642" href="StateSizedIO.LIB.DB.Query.html#2630" class="Bound">a</a> <a id="2644" class="Symbol">-&gt;</a> <a id="2647" href="Agda.Builtin.String.html#165" class="Postulate">String</a>
<a id="2654" href="StateSizedIO.LIB.DB.Query.html#2610" class="Function">showField</a> <a id="2664" class="Symbol">{</a><a id="2665" href="StateSizedIO.LIB.DB.Schema.html#487" class="InductiveConstructor">string</a><a id="2671" class="Symbol">}</a> <a id="2673" href="StateSizedIO.LIB.DB.Query.html#2673" class="Bound">s</a> <a id="2675" class="Symbol">=</a> <a id="2677" class="String">&quot;&#39;&quot;</a> <a id="2681" href="Data.String.Base.html#1101" class="Function Operator">+++</a> <a id="2685" href="StateSizedIO.LIB.DB.Query.html#2673" class="Bound">s</a> <a id="2687" href="Data.String.Base.html#1101" class="Function Operator">+++</a> <a id="2691" class="String">&quot;&#39;&quot;</a> <a id="2695" class="Comment">--listToString (vecToList s) +++ &quot;&#39;&quot;  -- TODO: escaping</a>
<a id="2751" href="StateSizedIO.LIB.DB.Query.html#2610" class="Function">showField</a> <a id="2761" class="Symbol">{</a><a id="2762" href="StateSizedIO.LIB.DB.Schema.html#509" class="InductiveConstructor">integer</a><a id="2769" class="Symbol">}</a> <a id="2771" href="StateSizedIO.LIB.DB.Query.html#2771" class="Bound">n</a> <a id="2773" class="Symbol">=</a> <a id="2775" href="Data.Integer.html#941" class="Function">showℤ</a> <a id="2781" href="StateSizedIO.LIB.DB.Query.html#2771" class="Bound">n</a>


<a id="2785" class="Comment">-- New row type without names [later perhaps NamedRows with the n</a>
<a id="2851" class="Comment">--</a>

<a id="2855" class="Comment">{-
showRow : forall {t} -&gt; Row t -&gt; String
showRow []        = &quot;&quot;
showRow (_∷_ {name = name} x []) = name +++ &quot; = &quot; +++ showField x
showRow (_∷_ {name = name} x xs) = name +++ &quot; = &quot; +++ showField x +++ &quot;, &quot; +++ showRow xs
-}</a>

<a id="showExpr"></a><a id="3081" href="StateSizedIO.LIB.DB.Query.html#3081" class="Function">showExpr</a> <a id="3090" class="Symbol">:</a> <a id="3092" class="Symbol">forall</a> <a id="3099" class="Symbol">{</a><a id="3100" href="StateSizedIO.LIB.DB.Query.html#3100" class="Bound">t</a> <a id="3102" href="StateSizedIO.LIB.DB.Query.html#3102" class="Bound">a</a><a id="3103" class="Symbol">}</a> <a id="3105" class="Symbol">-&gt;</a> <a id="3108" href="StateSizedIO.LIB.DB.Query.html#1112" class="Datatype">Expr</a> <a id="3113" href="StateSizedIO.LIB.DB.Query.html#3100" class="Bound">t</a> <a id="3115" href="StateSizedIO.LIB.DB.Query.html#3102" class="Bound">a</a> <a id="3117" class="Symbol">-&gt;</a> <a id="3120" href="Agda.Builtin.String.html#165" class="Postulate">String</a>
<a id="3127" href="StateSizedIO.LIB.DB.Query.html#3081" class="Function">showExpr</a> <a id="3136" class="Symbol">(</a><a id="3137" href="StateSizedIO.LIB.DB.Query.html#3137" class="Bound">e₁</a> <a id="3140" href="StateSizedIO.LIB.DB.Query.html#1159" class="InductiveConstructor Operator">===</a> <a id="3144" href="StateSizedIO.LIB.DB.Query.html#3144" class="Bound">e₂</a><a id="3146" class="Symbol">)</a> <a id="3148" class="Symbol">=</a> <a id="3150" href="StateSizedIO.LIB.DB.Query.html#3081" class="Function">showExpr</a> <a id="3159" href="StateSizedIO.LIB.DB.Query.html#3137" class="Bound">e₁</a> <a id="3162" href="Data.String.Base.html#1101" class="Function Operator">+++</a> <a id="3166" class="String">&quot; = &quot;</a> <a id="3172" href="Data.String.Base.html#1101" class="Function Operator">+++</a> <a id="3176" href="StateSizedIO.LIB.DB.Query.html#3081" class="Function">showExpr</a> <a id="3185" href="StateSizedIO.LIB.DB.Query.html#3144" class="Bound">e₂</a>
<a id="3188" href="StateSizedIO.LIB.DB.Query.html#3081" class="Function">showExpr</a> <a id="3197" class="Symbol">(</a><a id="3198" href="StateSizedIO.LIB.DB.Query.html#1219" class="InductiveConstructor Operator">!</a> <a id="3200" href="StateSizedIO.LIB.DB.Query.html#3200" class="Bound">x</a><a id="3201" class="Symbol">)</a> <a id="3203" class="Symbol">=</a> <a id="3205" href="StateSizedIO.LIB.DB.Query.html#3200" class="Bound">x</a>
<a id="3207" href="StateSizedIO.LIB.DB.Query.html#3081" class="Function">showExpr</a> <a id="3216" class="Symbol">(</a><a id="3217" href="StateSizedIO.LIB.DB.Query.html#1296" class="InductiveConstructor">val</a> <a id="3221" href="StateSizedIO.LIB.DB.Query.html#3221" class="Bound">c</a><a id="3222" class="Symbol">)</a> <a id="3224" class="Symbol">=</a> <a id="3226" href="StateSizedIO.LIB.DB.Query.html#2610" class="Function">showField</a> <a id="3236" href="StateSizedIO.LIB.DB.Query.html#3221" class="Bound">c</a>

<a id="showSQL"></a><a id="3239" href="StateSizedIO.LIB.DB.Query.html#3239" class="Function">showSQL</a> <a id="3247" class="Symbol">:</a> <a id="3249" href="StateSizedIO.LIB.DB.Query.html#2294" class="Datatype">SQL</a> <a id="3253" class="Symbol">-&gt;</a> <a id="3256" href="Agda.Builtin.String.html#165" class="Postulate">String</a>
<a id="3263" class="Comment">-- no where clause if that part is missing (pattern match):</a>
<a id="3323" href="StateSizedIO.LIB.DB.Query.html#3239" class="Function">showSQL</a> <a id="3331" class="Symbol">(</a><a id="3332" href="StateSizedIO.LIB.DB.Query.html#2312" class="InductiveConstructor Operator">SELECT</a> <a id="3339" href="StateSizedIO.LIB.DB.Query.html#3339" class="Bound">as</a> <a id="3342" href="StateSizedIO.LIB.DB.Query.html#2312" class="InductiveConstructor Operator">FROM</a> <a id="3347" href="StateSizedIO.LIB.DB.Query.html#3347" class="Bound">ts</a> <a id="3350" href="StateSizedIO.LIB.DB.Query.html#2312" class="InductiveConstructor Operator">WHERE</a> <a id="3356" href="Agda.Builtin.List.html#117" class="InductiveConstructor">[]</a><a id="3358" class="Symbol">)</a> <a id="3360" class="Symbol">=</a>
  <a id="3364" class="String">&quot;SELECT &quot;</a> <a id="3374" href="Data.String.Base.html#1101" class="Function Operator">+++</a> <a id="3378" href="StateSizedIO.LIB.DB.Query.html#2490" class="Function">sepBy</a> <a id="3384" class="String">&quot;, &quot;</a> <a id="3389" href="StateSizedIO.LIB.DB.Query.html#3339" class="Bound">as</a> <a id="3392" href="Data.String.Base.html#1101" class="Function Operator">+++</a> <a id="3396" class="String">&quot; FROM &quot;</a> <a id="3405" href="Data.String.Base.html#1101" class="Function Operator">+++</a> <a id="3409" href="StateSizedIO.LIB.DB.Query.html#2490" class="Function">sepBy</a> <a id="3415" class="String">&quot;, &quot;</a> <a id="3420" href="StateSizedIO.LIB.DB.Query.html#3347" class="Bound">ts</a>
<a id="3423" href="StateSizedIO.LIB.DB.Query.html#3239" class="CatchallClause Function">showSQL</a><a id="3430" class="CatchallClause"> </a><a id="3431" class="CatchallClause Symbol">(</a><a id="3432" href="StateSizedIO.LIB.DB.Query.html#2312" class="CatchallClause InductiveConstructor Operator">SELECT</a><a id="3438" class="CatchallClause"> </a><a id="3439" href="StateSizedIO.LIB.DB.Query.html#3439" class="CatchallClause Bound">as</a><a id="3441" class="CatchallClause"> </a><a id="3442" href="StateSizedIO.LIB.DB.Query.html#2312" class="CatchallClause InductiveConstructor Operator">FROM</a><a id="3446" class="CatchallClause"> </a><a id="3447" href="StateSizedIO.LIB.DB.Query.html#3447" class="CatchallClause Bound">ts</a><a id="3449" class="CatchallClause"> </a><a id="3450" href="StateSizedIO.LIB.DB.Query.html#2312" class="CatchallClause InductiveConstructor Operator">WHERE</a><a id="3455" class="CatchallClause"> </a><a id="3456" href="StateSizedIO.LIB.DB.Query.html#3456" class="CatchallClause Bound">cs</a><a id="3458" class="CatchallClause Symbol">)</a> <a id="3460" class="Symbol">=</a>
  <a id="3464" class="String">&quot;SELECT &quot;</a> <a id="3474" href="Data.String.Base.html#1101" class="Function Operator">+++</a> <a id="3478" href="StateSizedIO.LIB.DB.Query.html#2490" class="Function">sepBy</a> <a id="3484" class="String">&quot;, &quot;</a> <a id="3489" href="StateSizedIO.LIB.DB.Query.html#3439" class="Bound">as</a> <a id="3492" href="Data.String.Base.html#1101" class="Function Operator">+++</a> <a id="3496" class="String">&quot; FROM &quot;</a> <a id="3505" href="Data.String.Base.html#1101" class="Function Operator">+++</a> <a id="3509" href="StateSizedIO.LIB.DB.Query.html#2490" class="Function">sepBy</a> <a id="3515" class="String">&quot;, &quot;</a> <a id="3520" href="StateSizedIO.LIB.DB.Query.html#3447" class="Bound">ts</a> <a id="3523" href="Data.String.Base.html#1101" class="Function Operator">+++</a>
  <a id="3529" class="String">&quot; WHERE &quot;</a> <a id="3539" href="Data.String.Base.html#1101" class="Function Operator">+++</a> <a id="3543" href="StateSizedIO.LIB.DB.Query.html#2490" class="Function">sepBy</a> <a id="3549" class="String">&quot; AND &quot;</a> <a id="3557" href="StateSizedIO.LIB.DB.Query.html#3456" class="Bound">cs</a>

<a id="toSQL"></a><a id="3561" href="StateSizedIO.LIB.DB.Query.html#3561" class="Function">toSQL</a> <a id="3567" class="Symbol">:</a> <a id="3569" class="Symbol">forall</a> <a id="3576" class="Symbol">{</a><a id="3577" href="StateSizedIO.LIB.DB.Query.html#3577" class="Bound">s</a> <a id="3579" href="StateSizedIO.LIB.DB.Query.html#3579" class="Bound">t</a><a id="3580" class="Symbol">}</a> <a id="3582" class="Symbol">-&gt;</a> <a id="3585" href="StateSizedIO.LIB.DB.Query.html#1836" class="Datatype">Query</a> <a id="3591" href="StateSizedIO.LIB.DB.Query.html#3577" class="Bound">s</a> <a id="3593" href="StateSizedIO.LIB.DB.Query.html#3579" class="Bound">t</a> <a id="3595" class="Symbol">-&gt;</a> <a id="3598" href="StateSizedIO.LIB.DB.Query.html#2294" class="Datatype">SQL</a>
<a id="3602" href="StateSizedIO.LIB.DB.Query.html#3561" class="Function">toSQL</a> <a id="3608" class="Symbol">{</a><a id="3609" href="StateSizedIO.LIB.DB.Query.html#3609" class="Bound">s</a><a id="3610" class="Symbol">}</a> <a id="3612" class="Symbol">(</a><a id="3613" href="StateSizedIO.LIB.DB.Query.html#1882" class="InductiveConstructor">read</a> <a id="3618" href="StateSizedIO.LIB.DB.Query.html#3618" class="Bound">x</a> <a id="3620" class="Symbol">{</a><a id="3621" href="StateSizedIO.LIB.DB.Query.html#3621" class="Bound">p</a><a id="3622" class="Symbol">})</a> <a id="3625" class="Symbol">=</a> <a id="3627" href="StateSizedIO.LIB.DB.Query.html#2312" class="InductiveConstructor Operator">SELECT</a> <a id="3634" class="Symbol">(</a><a id="3635" href="Data.List.Base.html#1057" class="Function">Data.List.map</a> <a id="3649" href="Data.Product.html#559" class="Field">fst</a> <a id="3653" class="Symbol">(</a><a id="3654" href="StateSizedIO.LIB.DB.Dictionary.html#537" class="Function">lookup</a> <a id="3661" href="StateSizedIO.LIB.DB.Query.html#3618" class="Bound">x</a> <a id="3663" href="StateSizedIO.LIB.DB.Query.html#3609" class="Bound">s</a> <a id="3665" class="Symbol">{</a><a id="3666" href="StateSizedIO.LIB.DB.Query.html#3621" class="Bound">p</a><a id="3667" class="Symbol">}))</a> <a id="3671" href="StateSizedIO.LIB.DB.Query.html#2312" class="InductiveConstructor Operator">FROM</a> <a id="3676" href="StateSizedIO.LIB.DB.Query.html#3618" class="Bound">x</a> <a id="3678" href="Agda.Builtin.List.html#132" class="InductiveConstructor Operator">∷</a> <a id="3680" href="Agda.Builtin.List.html#117" class="InductiveConstructor">[]</a> <a id="3683" href="StateSizedIO.LIB.DB.Query.html#2312" class="InductiveConstructor Operator">WHERE</a> <a id="3689" href="Agda.Builtin.List.html#117" class="InductiveConstructor">[]</a>
<a id="3692" href="StateSizedIO.LIB.DB.Query.html#3561" class="Function">toSQL</a> <a id="3698" class="Symbol">(</a><a id="3699" href="StateSizedIO.LIB.DB.Query.html#3699" class="Bound">q₁</a> <a id="3702" href="StateSizedIO.LIB.DB.Query.html#1953" class="InductiveConstructor Operator">⊗</a> <a id="3704" href="StateSizedIO.LIB.DB.Query.html#3704" class="Bound">q₂</a><a id="3706" class="Symbol">)</a> <a id="3708" class="Keyword">with</a> <a id="3713" href="StateSizedIO.LIB.DB.Query.html#3561" class="Function">toSQL</a> <a id="3719" href="StateSizedIO.LIB.DB.Query.html#3699" class="Bound">q₁</a> <a id="3722" class="Symbol">|</a> <a id="3724" href="StateSizedIO.LIB.DB.Query.html#3561" class="Function">toSQL</a> <a id="3730" href="StateSizedIO.LIB.DB.Query.html#3704" class="Bound">q₂</a>
<a id="3733" class="Symbol">...</a> <a id="3737" class="Symbol">|</a> <a id="3739" href="StateSizedIO.LIB.DB.Query.html#2312" class="InductiveConstructor Operator">SELECT</a> <a id="3746" href="StateSizedIO.LIB.DB.Query.html#3746" class="Bound">as₁</a> <a id="3750" href="StateSizedIO.LIB.DB.Query.html#2312" class="InductiveConstructor Operator">FROM</a> <a id="3755" href="StateSizedIO.LIB.DB.Query.html#3755" class="Bound">ts₁</a> <a id="3759" href="StateSizedIO.LIB.DB.Query.html#2312" class="InductiveConstructor Operator">WHERE</a> <a id="3765" href="StateSizedIO.LIB.DB.Query.html#3765" class="Bound">cs₁</a>
    <a id="3773" class="Symbol">|</a> <a id="3775" href="StateSizedIO.LIB.DB.Query.html#2312" class="InductiveConstructor Operator">SELECT</a> <a id="3782" href="StateSizedIO.LIB.DB.Query.html#3782" class="Bound">as₂</a> <a id="3786" href="StateSizedIO.LIB.DB.Query.html#2312" class="InductiveConstructor Operator">FROM</a> <a id="3791" href="StateSizedIO.LIB.DB.Query.html#3791" class="Bound">ts₂</a> <a id="3795" href="StateSizedIO.LIB.DB.Query.html#2312" class="InductiveConstructor Operator">WHERE</a> <a id="3801" href="StateSizedIO.LIB.DB.Query.html#3801" class="Bound">cs₂</a> <a id="3805" class="Symbol">=</a> <a id="3807" href="StateSizedIO.LIB.DB.Query.html#2312" class="InductiveConstructor Operator">SELECT</a> <a id="3814" class="Symbol">(</a><a id="3815" href="StateSizedIO.LIB.DB.Query.html#3746" class="Bound">as₁</a> <a id="3819" href="Data.List.Base.html#1391" class="Function Operator">++</a> <a id="3822" href="StateSizedIO.LIB.DB.Query.html#3782" class="Bound">as₂</a><a id="3825" class="Symbol">)</a> <a id="3827" href="StateSizedIO.LIB.DB.Query.html#2312" class="InductiveConstructor Operator">FROM</a> <a id="3832" class="Symbol">(</a><a id="3833" href="StateSizedIO.LIB.DB.DBUniverse.html#1715" class="Function">nub</a> <a id="3837" class="Symbol">(</a><a id="3838" href="StateSizedIO.LIB.DB.Query.html#3755" class="Bound">ts₁</a> <a id="3842" href="Data.List.Base.html#1391" class="Function Operator">++</a> <a id="3845" href="StateSizedIO.LIB.DB.Query.html#3791" class="Bound">ts₂</a><a id="3848" class="Symbol">))</a> <a id="3851" href="StateSizedIO.LIB.DB.Query.html#2312" class="InductiveConstructor Operator">WHERE</a> <a id="3857" class="Symbol">(</a><a id="3858" href="StateSizedIO.LIB.DB.Query.html#3765" class="Bound">cs₁</a> <a id="3862" href="Data.List.Base.html#1391" class="Function Operator">++</a> <a id="3865" href="StateSizedIO.LIB.DB.Query.html#3801" class="Bound">cs₂</a><a id="3868" class="Symbol">)</a>
<a id="3870" href="StateSizedIO.LIB.DB.Query.html#3561" class="Function">toSQL</a> <a id="3876" class="Symbol">(</a><a id="3877" href="StateSizedIO.LIB.DB.Query.html#2056" class="InductiveConstructor">proj</a> <a id="3882" href="StateSizedIO.LIB.DB.Query.html#3882" class="Bound">as</a> <a id="3885" href="StateSizedIO.LIB.DB.Query.html#3885" class="Bound">q</a><a id="3886" class="Symbol">)</a> <a id="3888" class="Keyword">with</a> <a id="3893" href="StateSizedIO.LIB.DB.Query.html#3561" class="Function">toSQL</a> <a id="3899" href="StateSizedIO.LIB.DB.Query.html#3885" class="Bound">q</a>
<a id="3901" class="Symbol">...</a> <a id="3905" class="Symbol">|</a> <a id="3907" href="StateSizedIO.LIB.DB.Query.html#2312" class="InductiveConstructor Operator">SELECT</a> <a id="3914" class="Symbol">_</a> <a id="3916" href="StateSizedIO.LIB.DB.Query.html#2312" class="InductiveConstructor Operator">FROM</a> <a id="3921" href="StateSizedIO.LIB.DB.Query.html#3921" class="Bound">ts</a> <a id="3924" href="StateSizedIO.LIB.DB.Query.html#2312" class="InductiveConstructor Operator">WHERE</a> <a id="3930" href="StateSizedIO.LIB.DB.Query.html#3930" class="Bound">cs</a> <a id="3933" class="Symbol">=</a> <a id="3935" href="StateSizedIO.LIB.DB.Query.html#2312" class="InductiveConstructor Operator">SELECT</a> <a id="3942" class="Bound">as</a> <a id="3945" href="StateSizedIO.LIB.DB.Query.html#2312" class="InductiveConstructor Operator">FROM</a> <a id="3950" href="StateSizedIO.LIB.DB.Query.html#3921" class="Bound">ts</a> <a id="3953" href="StateSizedIO.LIB.DB.Query.html#2312" class="InductiveConstructor Operator">WHERE</a> <a id="3959" href="StateSizedIO.LIB.DB.Query.html#3930" class="Bound">cs</a>
<a id="3962" href="StateSizedIO.LIB.DB.Query.html#3561" class="Function">toSQL</a> <a id="3968" class="Symbol">(</a><a id="3969" href="StateSizedIO.LIB.DB.Query.html#2154" class="InductiveConstructor">select</a> <a id="3976" href="StateSizedIO.LIB.DB.Query.html#3976" class="Bound">c</a> <a id="3978" href="StateSizedIO.LIB.DB.Query.html#3978" class="Bound">q</a><a id="3979" class="Symbol">)</a> <a id="3981" class="Keyword">with</a> <a id="3986" href="StateSizedIO.LIB.DB.Query.html#3561" class="Function">toSQL</a> <a id="3992" href="StateSizedIO.LIB.DB.Query.html#3978" class="Bound">q</a>
<a id="3994" class="Symbol">...</a> <a id="3998" class="Symbol">|</a> <a id="4000" href="StateSizedIO.LIB.DB.Query.html#2312" class="InductiveConstructor Operator">SELECT</a> <a id="4007" href="StateSizedIO.LIB.DB.Query.html#4007" class="Bound">as</a> <a id="4010" href="StateSizedIO.LIB.DB.Query.html#2312" class="InductiveConstructor Operator">FROM</a> <a id="4015" href="StateSizedIO.LIB.DB.Query.html#4015" class="Bound">ts</a> <a id="4018" href="StateSizedIO.LIB.DB.Query.html#2312" class="InductiveConstructor Operator">WHERE</a> <a id="4024" href="StateSizedIO.LIB.DB.Query.html#4024" class="Bound">cs</a> <a id="4027" class="Symbol">=</a> <a id="4029" href="StateSizedIO.LIB.DB.Query.html#2312" class="InductiveConstructor Operator">SELECT</a> <a id="4036" href="StateSizedIO.LIB.DB.Query.html#4007" class="Bound">as</a> <a id="4039" href="StateSizedIO.LIB.DB.Query.html#2312" class="InductiveConstructor Operator">FROM</a> <a id="4044" href="StateSizedIO.LIB.DB.Query.html#4015" class="Bound">ts</a> <a id="4047" href="StateSizedIO.LIB.DB.Query.html#2312" class="InductiveConstructor Operator">WHERE</a> <a id="4053" class="Symbol">(</a><a id="4054" href="StateSizedIO.LIB.DB.Query.html#3081" class="Function">showExpr</a> <a id="4063" class="Bound">c</a> <a id="4065" href="Agda.Builtin.List.html#132" class="InductiveConstructor Operator">∷</a> <a id="4067" href="StateSizedIO.LIB.DB.Query.html#4024" class="Bound">cs</a><a id="4069" class="Symbol">)</a>
</pre></body></html>